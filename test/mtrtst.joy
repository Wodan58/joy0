(*
    module  : mtrtst.joy
    version : 1.1
    date    : 06/28/22
*)
DEFINE
putchars == [putch] step;
vv-samesize == [size] dip size =;
vv-bin-v == mapr2;
vv-bin-m == [map popd] cons cons map;
vv-2bin-s == [mapr2 uncons] dip step;
vs-bin-v == cons map;
v-zdiag-m == [small] [[null] [] [[] cons] ifte] [uncons]
[dup [first rest 0 swons cons] dip [0 swons] map cons] linrec;
v-negate-v == 0 swap [-] sv-bin-v;
v-invert-v == 1 swap [/] sv-bin-v;
v-e-diag-m == dup [[swons cons] cons [first rest] swoncat] dip [swons]
              cons [map cons] cons [dip] swoncat cons [dup] swoncat
              [[small] [[null] [] [[] cons] ifte] [uncons]] dip linrec;
v-1row-m == [] cons;
v-1col-m == [] swap [dupd swons] sv-bin-v;
unconsd == [uncons] dip;
uncons2 == unconsd uncons swapd;
transpose == [[null] [true] [[null] some] ifte] [pop []]
             [[[first] map] [[rest] map] cleave] [cons] linrec;
swoncat == swap concat;
sv-bin-v == map popd;
sm-bin-m == [(* sv-bin-v *) map] cons map popd; (* comment *)
radians == pi * 180 /;
putlist == "[" putchars [null] [pop] [unswons put
           ["\n  " putchars put] step] ifte "]\n" putchars;
pop2 == pop pop;
pi == 314159265;
pairstep == [dupd] swoncat [step pop] cons cons step;
pairlist == [] cons cons;
nulld == [null] dip;
null2 == nulld null or;
n-e-vector == [] cons [times] cons cons [] swap infra;
ms-cbin-m == swapd sm-bin-m;
ms-bin-m == cons [map] cons map;
mm-vercat-m == concat;
mm-mul-m == transpose [[[[*] mapr2] unary 0 [+] fold] map popd] cons map;
mm-horcat-m == [concat] mapr2;
mm-bin-m == [mapr2] cons mapr2;
mm-add-m == [+] mm-bin-m;
mm-2bin-m == [fold] cons [[[mapr2] cons unary unswons] cons] dip
             concat [transpose] dip [map popd] cons cons map;
mapr2 == [[null2] [pop2 []] [uncons2]] dip [dip cons] cons linrec;
m-transpose-m == [[null] [true] [[null] some] ifte] [pop []]
                 [[[first] map] [[rest] map] cleave] [cons] linrec;
m-print == putlist;
id2 == 2 1 n-e-vector 0 v-e-diag-m;
dip2 == [dip] cons dip;
cartproduct == [[]] dip2 [pairlist swap [swons] dip] pairstep.

0 __settracegc.
"output from test file  mtrtst.joy  /* MATRICES and VECTORS */\n"
putchars. "user input is indented by one tab\n" putchars.
5 "hello" n-e-vector.
5 1 n-e-vector.
5 [1 2 3] [+] sv-bin-v.
313 [10 20 30] [*] sv-bin-v.
5 [1 2 3 4 5 6 7 8 9 10] [-] sv-bin-v.
[1 2 3 4 5 6 7 8 9 10] 5 [-] vs-bin-v.
[{1 2 3} {4 5 6} {7 8 9}] {2 4 6 8} [and] vs-bin-v.
314 [[dup] [dup dup *] [dup dup dup * *]] [i] sv-bin-v.
[1 2 3 4] [dup] [*] concat [i] vs-bin-v.
[11 22 33 44 55] v-negate-v.
[11 22 33 44 55] v-invert-v.
[1 2 3 4] [11 22 33] vv-samesize.
[1 2 3 4] [11 22 33 44] vv-samesize.
[1 2 3] [10 20 30] [+] vv-bin-v.
[1 2 3] [10 20 30] [*] vv-bin-v.
[{1 2 3} {2 3 4}] [{1 3 4} {4 5 6}] [or] vv-bin-v.
[{1 2 3} {2 3 4}] [{1 3 4} {4 5 6}] [and] vv-bin-v.
true false false true [] cons cons cons cons
true true false false [] cons cons cons cons [and] vv-bin-v.
[1 2 3] [1 2 3] [*] [+] vv-2bin-s.
[11 22 33] [33 44 55] [*] [+] vv-2bin-s.
[{1 2 3} {4 5 6}] [{2 3} {2 6}] [and] [or] vv-2bin-s.
[{1 2 3} {4 5 6}] [{2 3} {2 6}] [or] [and] vv-2bin-s.
[{1 2 3} {4 5 6}] [{2 3} {2 6}] [and] [and] vv-2bin-s.
[{1 2 3} {4 5 6}] [{2 3} {2 6}] [or] [or] vv-2bin-s.
true false true [] cons cons cons
false true true [] cons cons cons [and] [or] vv-2bin-s.
[1 2 3 4] v-1row-m.
[1 2 3 4] v-1col-m.
[1 2 3 4] v-zdiag-m.
[1 2 3 4] 0 v-e-diag-m.
["Peter" "Paul" "Mary"] "" v-e-diag-m.
[{1 2} {3 4 5} {6}] {} v-e-diag-m.
4 1 n-e-vector v-zdiag-m.
4 1 n-e-vector 0 v-e-diag-m.
4 'i n-e-vector 'o v-e-diag-m.
[1 2 3] [10 20] [+] vv-bin-m.
[11 22] [33 44 55 66] [+] vv-bin-m.
[11 22] [44 55 66 77] [*] vv-bin-m.
[{1 2 3} {4 5}] [{1} {2 3} {3 4}] [or] vv-bin-m.
[{1 2 3} {4 5}] [{1} {2 3} {3 4}] [and] vv-bin-m.
[1 2 3 4] [[id] [dup *] [dup dup * *]] [dupd i] vv-bin-m.
[peter paul] [[smith] [jones]] [cons] vv-bin-m.
[peter paul] [[smith] [jones]] [cons] vv-bin-m size.
[peter paul] [smith jones] cartproduct.
[peter paul] [smith jones] cartproduct size.
[[1 2] [3 4]] [[5 6] [7 8]] mm-vercat-m.
[[1 2] [3 4]] [[5 6] [7 8]] mm-horcat-m.
[[1 2 3] [4 5 6] [7 8 9] [10 11 12]] m-transpose-m.
[[1 2] [3 4] [5 6]] 10 [+] ms-cbin-m.
[[1 2] [3 4] [5 6]] 10 [-] ms-bin-m.
[[1 2] [3 4] [5 6]] 10 [*] ms-cbin-m.
[[1 2] [3 4] [5 6]] 0 [*] ms-cbin-m.
[[1 2] [3 4] [5 6]] 7 [popd] ms-bin-m.
[[1 2] [3 4] [5 6]] [succ] [i] ms-bin-m.
[[{1 2} {3 4}] [{5 6} {7 8}]] {1 3 5 7} [and] ms-bin-m.
5 [[1 2 3] [4 5 6] [7 8 9]] [-] sm-bin-m.
[[1 2 3] [4 5 6] [7 8 9]] 5 [-] ms-bin-m.
0 [[1 2 3] [4 5 6] [7 8 9]] [pop] sm-bin-m.
[[1 2 3] [4 5 6]] [[10 20 30] [40 50 60]] mm-add-m.
[[1 2 3] [4 5 6]] [[10 20 30] [40 50 60]] [+] mm-bin-m.
[[1 2 3] [4 5 6]] [[10 20 30] [40 50 60]] [*] mm-bin-m.
true true [] cons cons
false false [] cons cons [] cons cons
true false [] cons cons dup [] cons cons [or] mm-bin-m.
true true [] cons cons
false false [] cons cons [] cons cons
true false [] cons cons dup [] cons cons [and] mm-bin-m.
true true [] cons cons
false false [] cons cons [] cons cons
true false [] cons cons dup [] cons cons [xor] mm-bin-m.
id2.
id2 m-transpose-m.
id2 [[1 2 3] [4 5 6]] mm-mul-m.
[[1 2] [3 4] [5 6]] id2 mm-mul-m.
[[1 2] [3 4] [5 6]] id2 [*] [+] mm-2bin-m.
[[11 22] [33 44] [55 66]] m-print.
[[11 22] [33 44]] m-print.
[[11 22] [33 44] [55 66]] [[11 22] [33 44]] mm-mul-m m-print.
[[{1 2} {3 4}] [{4 5} {6 7}]] m-print.
[[{1 3 5} {6}] [{2} {3 4 5}]] m-print.
[[{1 2} {3 4}] [{4 5} {6 7}]] [[{1 3 5} {6}] [{2} {3 4 5}]] [and] [or]
mm-2bin-m m-print.
