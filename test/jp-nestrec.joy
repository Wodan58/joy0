(*
    module  : jp-nestrec.joy
    version : 1.1
    date    : 06/28/22
*)
DEFINE	nl == '\n putch;
y-ack == [[[[pop pop null] pop popd succ] [[pop null] [pop pred 1]
	 dip i] [[[dup pred swap] dip pred] dip twice-i]] cond] y;
y == [dup cons] swoncat dup cons i;
x-mcc91 == [[pop 100 >] [pop 10 -] [[11 +] dip twice-x] ifte] x;
x-hamilhyp == [[pop null] [pop pop] [dup [[dup rollup pred] dip x]
              dip [dupd cons] dip [swap pred] dip x] ifte] x;
x-fact == [[pop null] [pop pop 1] [[dup pred] dip x *] ifte] x;
x-ack == [[[[pop pop null] pop popd succ] [[pop null] [pop pred 1]
         dip x] [[[dup pred swap] dip pred] dip twice-x]] cond] x;
twice-x == dup [x] dip x;
twice-i == dup [i] dip i;
toggle == [has] [[not] dip swons not] [swons] ifte;
swoncat == swap concat;
r-mcc91 == [100 >] [10 -] [11 + r-mcc91 r-mcc91] ifte;
r-hamilhyp == [null] [pop] [dup rollup pred r-hamilhyp dupd cons swap
              pred r-hamilhyp] ifte;
r-fact == [null] [pop 1] [dup pred r-fact *] ifte;
r-ack == [[[pop null] popd succ] [[null] pop pred 1 r-ack]
         [[dup pred swap] dip pred r-ack r-ack]] cond;
lr-hamilhyp == [null] [pop] [dup rollup pred] [dupd cons swap pred
               lr-hamilhyp] linrec;
lr-grayseq == [null] [pop] [dup rollup pred] [dupd dup [first swap
              toggle] dip cons swap pred lr-grayseq] linrec;
l-mcc91 == [100 >] [10 -] [11 +] [l-mcc91] linrec;
dup2 == dupd dup swapd;
clr-ack == [[[pop null] [popd succ]] [[null] [pop pred 1] []]
           [[[dup pred swap] dip pred] [clr-ack]]] condlinrec;
ack == [[[0 =] [pop 1 +]] [[swap 0 =] [popd 1 - 1 swap] []]
       [[dup rollup [1 -] dip] [swap 1 - ack]]] condlinrec.

0 __settracegc.
[0 1 2 3 4 5 6] [r-fact] map.
[-7 42 99 100 101 102 345] [r-mcc91] map.
[[0 0] [0 1] [0 2] [0 3] [0 4] [0 5]] [i r-ack] map put nl
[[1 0] [1 1] [1 2] [1 3] [1 4] [1 5]] [i r-ack] map put nl
[[2 0] [2 1] [2 2] [2 3] [2 4] [2 5]] [i r-ack] map put nl
[[3 0] [3 1] [3 2] [3 3] [3 4] [3 5]] [i r-ack] map put nl
[[4 0]] [i r-ack] map.
[] 3 r-hamilhyp.
[] 4 r-hamilhyp.
[] 5 r-hamilhyp.
[0 1 2 3 4 5 6] [x-fact] map.
[-7 42 99 100 101 102 345] [x-mcc91] map.
[[3 0] [3 1] [3 2] [3 3] [3 4] [3 5]] [i x-ack] map.
[[3 0] [3 1] [3 2] [3 3] [3 4] [3 5]] [i y-ack] map.
[] 5 x-hamilhyp.
[[0 0] [0 1] [0 2] [0 3] [0 4] [0 5]] [i swap ack] map.
[[1 0] [1 1] [1 2] [1 3] [1 4] [1 5]] [i swap ack] map.
[[2 0] [2 1] [2 2] [2 3] [2 4] [2 5]] [i swap ack] map.
[[3 0] [3 1] [3 2] [3 3] [3 4] [3 5]] [i swap ack] map.
[[4 0]] [i swap ack] map.
[-7 42 99 100 101 102 345] [l-mcc91] map.
[[3 0] [3 1] [3 2] [3 3] [3 4] [3 5]] [i clr-ack] map.
[] 5 lr-hamilhyp.
{} 4 toggle.
{1 2 7} 2 toggle.
[{}] 3 lr-grayseq.
[{3}] 3 lr-grayseq.
[{1 2 3}] 3 lr-grayseq.
